buildscript {
    repositories {
        mavenCentral()
        jcenter()
    }

    dependencies {
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.7.3'
        classpath 'org.jfrog.buildinfo:build-info-extractor-gradle:4.4.18'
    }

}

plugins {
    id 'java'
    id 'groovy'
    id 'maven-publish'
    id 'java-gradle-plugin'
}

group 'com.novarto'
version '0.9.1-SNAPSHOT'

repositories {
    mavenCentral()
    jcenter()
}


def isSnapshot = version.toString().endsWith("SNAPSHOT")

if(isSnapshot)
{
    apply plugin: 'com.jfrog.artifactory'
}
else
{
    apply plugin: 'com.jfrog.bintray'

}


task('sourcesJar', type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}


artifacts {
    archives sourcesJar
}

dependencies {
    compile gradleApi()
    compile localGroovy()
    testCompile group: 'junit', name: 'junit', version: '4.12'
    testCompile gradleTestKit()

    // we add bintray as a runtime dependency, so that clients do not have to specify it via
    // { buildscript { dependencies
    runtime 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.7.3'
    runtime 'org.jfrog.buildinfo:build-info-extractor-gradle:4.4.18'

}

publishing {
    publications {


        mavenJava(MavenPublication) {
            from components.java
            artifact sourcesJar
            groupId project.group
            artifactId project.name
            version project.version
        }
    }
}

def publishUser = System.getenv('PUBLISH_USER')
def publishKey = System.getenv('PUBLISH_KEY')


if(isSnapshot) {
    artifactory {
        publish {
            contextUrl = 'https://oss.jfrog.org/artifactory'
            repository {
                repoKey = 'oss-snapshot-local'
                username = publishUser
                password = publishKey
            }

            defaults {
                publications('mavenJava')
            }
        }
    }
}
else {

    bintray {

        user = publishUser
        key = publishKey


        pkg {
            repo = 'novarto-oss-snapshots'
            name = 'java-build-conventions'
            userOrg = 'novarto-oss'
            licenses = ['Apache-2.0']
            vcsUrl = 'https://github.com/novarto-oss/java-build-conventions.git'

            version {
                name = project.version
            }
        }

        publications = ['mavenJava']

    }

}